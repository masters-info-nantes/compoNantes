grammar org.xtext.example.compoNantes.CompoNantes with org.eclipse.xtext.common.Terminals

generate compoNantes "http://www.xtext.org/example/compoNantes/CompoNantes"

Model:
	subsystem+=(SubSystem|Component)*;

SubSystem:
	'subsystem' name=ID '{'
		ports+=Port*
		interfaces+=Interface*
		components+=Component*
		dependencies+=Dependency*
		usages+=Usage*
		connectors+=Connector*
	'}'
;

Component:
	'component' name=ID '{'
		ports+=Port*
		interfaces+=Interface*
		dependencies+=Dependency*
		usages+=Usage*
		connectors+=Connector*
	'}'
	
;

Connector:
	'connector' type=ConnectorType name=ID ':' int1=[Interface|QualifiedName] 'to' int2=[Interface|QualifiedName]
;

Interface:
	'interface' type=InterfaceType name=ID 'port' port=[Port]
;
	
Port:
	'port' name=ID
;
	
Dependency:
	'dependency' component = [Element|QualifiedName]
;

Usage:
	'usage' component = [Element|QualifiedName]
;

Element:
	Component | SubSystem
;

QualifiedName:
    ID ('.' ID)*;

enum InterfaceType:
	REQUIRED='required' | PROVIDED='provided'
;

enum ConnectorType:
	ASSEMBLY='assembly' | DELEGATION='delegation'
;