grammar org.xtext.example.compoNantes.CompoNantes with org.eclipse.xtext.common.Terminals

generate compoNantes "http://www.xtext.org/example/compoNantes/CompoNantes"

Model:
	components+=Component*;

Component:
	'component' name=ID '{'
		ports+=Port*
		interfaces+=Interface*
		components+=Component*
		connectors+=Connector*
		dependencies+=Dependency*
	'}'
;

Port:
	'port' name=ID
;

Interface:
	'interface' type=InterfaceType name=ID 'port' port=[Port]
;

Connector:
	'connector' type=ConnectorType name=ID '{' 
		'from' int_from=[Interface|QualifiedName] 
		'to' int_to=[Interface|QualifiedName]
	'}'
;

Dependency:
	'dependency' type=DependencyType name=ID '{' 
		'from' component_from=[Interface|QualifiedName] 
		'to' component_to=[Interface|QualifiedName]
	'}'
;

QualifiedName:
    ID ('.' ID)*;

enum InterfaceType:
	REQUIRED='required' | PROVIDED='provided'
;

enum ConnectorType:
	ASSEMBLY='assembly' | DELEGATION='delegation'
;

enum DependencyType:
	USAGE='usage' | ASSOCIATION='association'
;